buildscript {
    ext.compileSdkVersion_ver = 23
    ext.buildToolsVersion_ver = '23.0.2'

    ext.minSdkVersion_ver = 21
    ext.targetSdkVersion_ver = 23
    ext.versionCode_ver = 1
    ext.versionName_ver = '1.0'

    ext.support_ver = '23.1.1'

    ext.kotlin_ver = '1.0.0'
    ext.anko_ver = '0.8.2'

    ext.glide_ver = '3.7.0'
    ext.retrofit_ver = '2.0.0-beta4'
    ext.rxjava_ver = '1.1.1'
    ext.rxandroid_ver = '1.1.0'

    ext.junit_ver = '4.12'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_ver"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion "$compileSdkVersion_ver".toInteger()
    buildToolsVersion "$buildToolsVersion_ver"

    defaultConfig {
        applicationId "com.github.cirorizzo.kshows"
        minSdkVersion "$minSdkVersion_ver".toInteger()
        targetSdkVersion "$targetSdkVersion_ver".toInteger()
        versionCode "$versionCode_ver".toInteger()
        versionName "$versionName_ver"

    }

    buildTypes {
        debug {
            buildConfigField("int", "MAX_IMAGES_PER_REQUEST", "10")
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField("int", "MAX_IMAGES_PER_REQUEST", "500")
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile "junit:junit:$junit_ver"

    compile "com.android.support:appcompat-v7:$support_ver"
    compile "com.android.support:cardview-v7:$support_ver"
    compile "com.android.support:recyclerview-v7:$support_ver"
    compile "com.github.bumptech.glide:glide:$glide_ver"

    compile "com.squareup.retrofit2:retrofit:$retrofit_ver"
    compile ("com.squareup.retrofit2:converter-simplexml:$retrofit_ver") {
        exclude module: 'xpp3'
        exclude group: 'stax'
    }

    compile "io.reactivex:rxjava:$rxjava_ver"
    compile "io.reactivex:rxandroid:$rxandroid_ver"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofit_ver"


    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_ver"
    compile "org.jetbrains.anko:anko-common:$anko_ver"
}

configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:$support_ver"
}

repositories {
    mavenCentral()
}
